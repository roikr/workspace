package {
	import flash.display.Sprite;
	import flash.events.Event;
	import flash.events.MouseEvent;
	import flash.events.ProgressEvent;
	import flash.media.SoundMixer;
	import flash.media.SoundTransform;
	import flash.net.URLRequest;

	/**
	 * @author roikr
	 */
	public class TestingGameManager extends Sprite implements IGameManager{
		
		public var gameUI : TestingGameUIAssets;
		public var game:IChatpetzGame;
		
		
		public function TestingGameManager() {
			addChild(gameUI=new TestingGameUIAssets());
		}
		
		public function load(url:String) : void {
			//var url:String = "ChatpetzClouds.swf";
			var req:URLRequest = new URLRequest(url);
			gameUI.uiLoader.scaleContent = false;
			gameUI.uiLoader.addEventListener(ProgressEvent.PROGRESS, progressHandler);
			gameUI.uiLoader.addEventListener(Event.COMPLETE, completeHandler);
			gameUI.uiLoader.load(req);
			gameUI.bStart.enabled = false;
			gameUI.bClose.enabled = false;
			gameUI.cbSound.selected = true;
		}
		
		public function unload() :void {
			game.exit();
			gameUI.uiLoader.unload();
			game = null;
			gameUI.bStart.enabled = false;	
			gameUI.bClose.enabled = false;	
		}
		
		public function close() : void {
			
		}

		private function progressHandler(e:ProgressEvent) : void {
			//trace("progressHandler: "+e.bytesLoaded+"/"+e.bytesTotal);
		}
		
		private function completeHandler(e:Event) : void {
			gameUI.bStart.enabled = true;
			gameUI.bStart.addEventListener(MouseEvent.MOUSE_DOWN,onStart);
			gameUI.bClose.addEventListener(MouseEvent.MOUSE_DOWN,onClose);
			gameUI.cbSound.addEventListener(Event.CHANGE, function() : void {SoundMixer.soundTransform = new SoundTransform(gameUI.cbSound.selected ? 1 : 0);} );
			game = gameUI.uiLoader.content as IChatpetzGame;
		}

		private function onStart(e:Event) : void {
			game.start(this);
			gameUI.bStart.enabled = false;	
			gameUI.bClose.enabled = true;		
		}
		
		public function onClose(e:Event) : void {
			unload();
			close();
			
			
		}
		
		public function setStars(stars:Number) : void {
			gameUI.laStars.text = stars.toPrecision(2);
		}

		public function setScore(score:int) : void {
			gameUI.laScore.text = score.toString();
		}
		
		public function setLevel(level:int) : void {
			gameUI.laLevel.text = level.toString();
		}
		
		public function setTime(time:int) : void {
			gameUI.laTime.text = time.toString();
		}

		public function playChatpetzCode(code : int,probability:Number=1.0) : void {
			ChatpetzBeeps.play(code,probability)
		}
		
		public function chooseAndPlayChatpetzCode(arr:Array,probability:Number=1.0) : void {
			ChatpetzBeeps.chooseAndPlay(arr,probability);	
		}
		
		
		
		
	}
}
